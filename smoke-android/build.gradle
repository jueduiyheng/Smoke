apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'
apply plugin: "com.jfrog.artifactory"

ext {
    groupId = 'com.mingyuans.android'
    artifactId = 'smoke-android'
    version = project.smoke_android_version

    siteUrl = "http://www.mingyuans.me"
    gitUrl = "https://github.com/mingyuans/Smoke.git"
}

group = project.ext.groupId
version = project.ext.version


android {
    compileSdkVersion 24
    buildToolsVersion '25.0.0'

    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 24
        versionCode 1
        versionName project.version
        consumerProguardFile 'proguard-rules.pro'
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        buildConfigField "String","SMOKE_ANDROID_VERSION","\"" + project.version  + "\""
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }


}


install {
    repositories.mavenInstaller {
        pom {
            project {
                packaging 'aar'
                url project.ext.siteUrl
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'mingyuans'
                        name 'mingyuans'
                        email 'yanxqwork@gmail.com'
                    }
                }
                scm {
                    connection project.ext.gitUrl
                    developerConnection project.ext.gitUrl
                    url project.ext.siteUrl
                }
            }
        }
    }
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

def boolean isSnapshot() {
    def versionString = project.version as String;
    return versionString.endsWith("SHAPSHOT");
}

Properties properties = new Properties()
if (project.rootProject.file("local.properties").exists()) {
    properties.load(project.rootProject.file("local.properties").newInputStream())
}
bintray {
    user = properties.getProperty("bintray.user","")
    key = properties.getProperty("bintray.apikey","")
    configurations = ['archives']
    override = true
    pkg {
        repo = "maven"
//        name = isSnapshot()? project.ext.artifactId + "-snapshot" : project.ext.artifactId;
        name = "smoke"
        websiteUrl = project.siteUrl
        vcsUrl = project.gitUrl
        licenses = ["Apache-2.0"]
        publish = true
    }
}


artifactory {
    contextUrl = 'http://oss.jfrog.org/artifactory'
    publish {
        repository {
//            repoKey = project.ext.version.endsWith('SNAPSHOT')?
//                    'oss-snapshot-local' : 'oss-release-local'
            repoKey = 'oss-snapshot-local';
            username = properties.getProperty("bintray.user","")
            password = properties.getProperty("bintray.apikey","")
            maven = true
        }

        defaults {
            publishConfigs('archives')
            publishIvy = false
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    testCompile 'junit:junit:4.12'
    androidTestCompile 'com.squareup.okhttp3:okhttp:3.6.0'
    compile 'com.mingyuans.android:smoke-java:2.1.2-SNAPSHOT'
//    compile project(':smoke-java')
}
